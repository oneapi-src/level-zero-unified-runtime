# Copyright (C) 2022-2023 Intel Corporation
# Part of the Unified-Runtime Project, under the Apache License v2.0 with LLVM Exceptions.
# See LICENSE.TXT
# SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception

set(UR_UMA_TEST_DIR ${CMAKE_CURRENT_SOURCE_DIR})

add_library(uma_test_helpers STATIC
    ${UR_UMA_TEST_DIR}/common/pool.c
    ${UR_UMA_TEST_DIR}/common/provider.c
)

target_link_libraries(uma_test_helpers PRIVATE ${PROJECT_NAME}::common)

function(add_uma_test name)
    set(TEST_TARGET_NAME uma_test-${name})
    add_executable(${TEST_TARGET_NAME}
        ${ARGN})
    target_link_libraries(${TEST_TARGET_NAME}
        PRIVATE
        ${PROJECT_NAME}::unified_memory_allocation
        ${PROJECT_NAME}::common
        uma_test_helpers
        GTest::gtest_main)
    target_include_directories(${TEST_TARGET_NAME} PRIVATE
        ${UR_UMA_TEST_DIR}/common)
    add_test(NAME  uma-${name}
        COMMAND ${TEST_TARGET_NAME}
        WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})
    set_tests_properties(uma-${name} PROPERTIES LABELS "uma")
endfunction()

add_subdirectory(uma_pools)

add_uma_test(memoryProvider memoryProviderAPI.cpp)
add_uma_test(memoryPool memoryPoolAPI.cpp)
add_uma_test(base base.cpp)
